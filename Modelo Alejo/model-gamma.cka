%use:
#Parameters
%var: 'infected-transmission' 0.25
%var: 'dead-transmission' 0.2
%var: 'case-fatality-proportion' 0.7

## Exposure has a gamma distribution
%var: 'exposure-time' 11.0
%var: 'exposure-stages' 6
%var: 'exposure-rate' 'exposure-stages'/'exposure-time'

## Death and infection have exponential distributions
### Maybe should change to gamma matching above?
%var: 'Dead_contagious-time' 4.0
%var: 'Infected-time' 6.0

#Agents
#%agent: Person(c~S~E~I, info~zero~neg~pos,d)
%agent: Person(c~S~E~I~R,d)
%agent: Dead(d~I~R)
%agent: Day(i1,i2)

#Vars
%var: 'S' Person(c~S)
%var: 'E' Person(c~E)
%var: 'I' Person(c~I)
%var: 'R' Person(c~R)
%var: 'D' Dead()
%var: 'DR' Dead(d~R)
%var: 'N' 'S'+'E'+'I'+'R'
%var: 'infectious_live' 'infected-transmission'/'N' 
%var: 'infectious_dead' 'dead-transmission'/'N'

##################
# RATES INFECTION#
#################
# Transmission rates for different information groups
%var: 'S I_rate' 'infectious_live'
%var: 'S D_rate' 'infectious_dead'

##########
# RULES #
##########
# infected to susceptible
'S I' Person(c~S,d),Person(c~I) -> Person(c~E,d!1),Person(c~I),Day(i1!1) @ 'S I_rate'

#dead to susceptible
'S D' Person(c~S,d),Dead(d~I) -> Person(c~E,d!1),Dead(d~I),Day(i1!1) @ 'S D_rate'

#########
# Time #
########
'replication' Day(i1!_,i2) -> Day(i1!_,i2!1),Day(i1!1,i2) @ 'exposure-rate'
'infect' Person(c~E,d!0),Day(i1!0,i2!1),Day(i1!1,i2!2),Day(i1!2,i2!3),Day(i1!3,i2!4),Day(i1!4,i2!5),Day(i1!5,i2!6),Day(i1!6,i2) ->\
			Person(c~I,d) @ [inf]

####################
#Die, live and bury#
###################
'Die' Person(c~I) -> Dead(d~I) @ 'case-fatality-proportion'/'Infected-time'
'live' Person(c~I) -> Person(c~R) @ (1-'case-fatality-proportion')/'Infected-time'
'bury' Dead(d~I) -> Dead(d~R) @ 1/'Dead_contagious-time' 

#Clean
'remove days' Day(i1) ->  @ [inf]
